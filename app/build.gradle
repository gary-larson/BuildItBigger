apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.3'
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        free {
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
        }
        paid {
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    freeImplementation 'com.google.android.gms:play-services-ads:19.3.0'
    implementation 'com.google.api-client:google-api-client:1.23.0'
    implementation 'com.google.http-client:google-http-client-android:1.23.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation project (':jokelibrary')
    implementation project (':showjokelibrary')

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // Espresso Dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'

    testImplementation 'junit:junit:4.13'
}

task startAppengineRun {
    dependsOn ':backend:appengineStart'
    doLast {
        println "App Engine Started"
    }
}

task runUnitTests {
    dependsOn "test"
    doLast {
        println "Unit Tests Done"
    }
}

task runAndroidTests {
    dependsOn "connectedAndroidTest"
    doLast {
        println "Android Tests Done"
    }
}

task stopAppengineRun {
    dependsOn ':backend:appengineStop'
    doLast {
        println "App Engine Stopped"
    }
}

task runAllMyTasks {
    dependsOn = ["startAppengineRun", "runUnitTests", "runAndroidTests", "stopAppengineRun"]
    tasks.findByName("runUnitTests").mustRunAfter "startAppengineRun"
    tasks.findByName("runAndroidTests").mustRunAfter "runUnitTests"
    tasks.findByName("stopAppengineRun").mustRunAfter "runAndroidTests"
    doLast {
        println "Completed All Tasks"
    }
}

